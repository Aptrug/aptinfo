#!/usr/bin/env -S gawk -E

@include "getopt.awk"

# Requirements:
# * `apt` >= 0.9.9.1
# * `dpkg` >= 1.19.3
# * `gawk` >= 3.1
# * `fzf` >= 0.13.0 (optional)

function usage()
{
	OFS = "\n"
	print "usage: apt_info [-1RUbdeflmrs] [-P PKG]",
		  "\nsort:",
		  "b\tbinaries",
		  "d\tdependencies",
		  "e\tessential",
		  "f\tfiles",
		  "l\tlast modification date",
		  "m\tmanpages",
		  "r\treverse dependencies",
		  "s\tsize",
		  "\nmisc:",
		  "1\toutput reversed",
		  "P\tshow individual package info",
		  "U\tshow unneeded packages",
		  "R\toutput without fzf"
	exit
}

# Will be run by the preview window of fzf using '-P' option
function fzf_preview(package)
{
	output = ""
	cmd = "apt show " package " 2>/dev/null"
	system(cmd)

	cmd = "dpkg-query --listfiles " package
	manpage_regex = "^/data/data/com.termux/files/usr/share/man/man[1-8]/"
	while ((cmd | getline) > 0) {
		if (/^\/data\/data\/com.termux\/files\/usr\/bin$/) {
			output = output "\nBinaries:"
		} else if (/^\/data\/data\/com.termux\/files\/usr\/share\/man$/) {
			output = output "\nManpages:"
		} else if (sub(manpage_regex, "")) {
			sub(/\..*/, "")
			output = output " " $0
		} else if (sub(/\/data\/data\/com.termux\/files\/usr\/bin\//, "")) {
			output = output " " $0
	}
	}
	close(cmd)

	cmd = "apt-cache rdepends --important --installed "\
					"--implicit --recurse " package
	split("", recurse_reverse_dependencies)
	cmd | getline
	while((cmd | getline) > 0) {
		if (/^Reverse Depends:$/) {
			continue
		}
		sub(/^ +/, "")
		recurse_reverse_dependencies[$0] = ""
	}
	close(cmd)
	cmd = "apt-cache depends --important --installed "\
					"--implicit --recurse " package
	split("", recurse_dependencies)
	cmd | getline
	while((cmd | getline) > 0) {
		sub(/^  Depends: /, "")
		recurse_dependencies[$0] = ""
	}
	close(cmd)

	array_length = length(recurse_dependencies)
	if (array_length) {
		output = output "\nTotal Dependencies (" array_length "): "
		for (i in recurse_dependencies) {
			output = output i " "
		}
	}
	array_length = length(recurse_reverse_dependencies)
	if (array_length) {
		output = output "\nTotal Reverse Dependencies (" array_length "): "
		for (i in recurse_reverse_dependencies) {
			output = output i " "
		}
	}
	output = output "\n"

	# Unlike [[:space:]] and " ", [[:blank:]] does not match "\n".
	split(output, arr, "[[:blank:]]")
	termWinMaxCol = 55
	line_length = 0
	for (i in arr) {
		j = arr[i]
		line_length += length(j)
		if (line_length >= termWinMaxCol) {
			printf "\n  "
			line_length = 0
		} else if (j ~ /\n/) {
			line_length = 0
		}
		printf "%s ", j
	}
	exit
}

function process_options()
{
	show_without_fzf = 0
	order_method = "@val_num_desc"
	supported_sort_options = "bdeflmrs"
	supported_options = "1P:RU" supported_sort_options
	unneeded_only = 0
	sort_method = ""
	Opterr = ""
	while ((C = getopt(ARGC, ARGV, supported_options)) != -1) {
		C == "1" ? order_method = "@val_num_asc" :
		C == "P" ? fzf_preview(Optarg) :
		C == "R" ? show_without_fzf = 1 :
		C == "U" ? unneeded_only = 1 :
		supported_sort_options ~ C ? sort_method = Optopt :
		usage()
	}
	# Exit if no valid sort option is provided
	if (!(sort_method)) {
		usage()
	}
}

function main()
{
	# Array initializations
	split("", essential)
	split("", dependencies)
	split("", reverse_dependencies)
	split("", binaries)
	split("", manpages)
	split("", files)
	split("", size)

	longest_package_name = 0
	packages = ""
	cmd = "dpkg-query --show --showformat='${db:Status-Want}\n"\
		"${Package}\n${Essential}\n${Installed-Size}\n"\
		"${db-fsys:Last-Modified}\n${Depends}\n${db-fsys:Files}'"
	while ((cmd | getline) > 0) {
		if (/^install$/) {
			flag = 1
		} else if (flag == 1) {
			package = $0
			packages = packages " " package

			package_name_length = length(package)
			if (package_name_length > longest_package_name) {
				longest_package_name = package_name_length
			}

			flag = 2
		} else if (flag == 2) {
			essential[package] = /^yes$/ ? 69 : 32
			flag = 3
		} else if (flag == 3) {
			size[package] = sprintf("%.2f MiB", $0 / 1024)
			flag = 4
		} else if (flag == 4) {
			last_modified[package] = $0
			flag = 5
		} else if (flag == 5) {
			flag = 6
			if ($0) {
				dependencies[package] = 1
			} else {
				dependencies[package] = 0
				continue
			}
			for (i = 1; i <= NF; ++i) {
				if ($i ~ /,/) {
					++dependencies[package]
				}
			}
			manpages[package] = 0
			binaries[package] = 0
			files[package] = 0
		} else if (flag == 6) {
			++files[package]
			if (/\/usr\/share\/man\/man[1-8]\/[^/]+$/) {
				++manpages[package]
			} else if (/\/usr\/bin.*[^/]$/) {
				++binaries[package]
			}
		}
	}
	close(cmd)

	cmd = "apt-cache rdepends --important --installed" packages
	while ((cmd | getline) > 0) {
		if (/^ /) {
			++reverse_dependencies[package]
		} else if (/^</) {
			break
		} else if ($0 != "Reverse Depends:") {
			package = $0
			reverse_dependencies[package] = 0
		}
	}
	close(cmd)

	n = sort_method == "b" ? asorti(binaries, sorted, order_method)	:
	sort_method == "d" ? asorti(dependencies, sorted, order_method) :
	sort_method == "e" ? asorti(essential, sorted, order_method) :
	sort_method == "f" ? asorti(files, sorted, order_method) :
	sort_method == "l" ? asorti(last_modified, sorted, order_method) :
	sort_method == "m" ? asorti(manpages, sorted, order_method) :
	sort_method == "r" ? asorti(reverse_dependencies, sorted, order_method) :
	asorti(size, sorted, order_method)

	output = ""
	for (i = 1; i <= n; ++i) {
		j = sorted[i]

		# Parentheses are needed
		if (unneeded_only && (essential[j] == 69 ||
			reverse_dependencies[j] != 0)) {
				continue
		}

		output = output\
		sprintf("%c| %.*s %-3s | %-4s | %-4s | %-5s | %-5s | %s\n",
		essential[j],
		longest_package_name,
		j " ...........................................",
		dependencies[j] "D",
		reverse_dependencies[j] "R",
		binaries[j] "B",
		manpages[j] "M",
		files[j] "F",
		size[j])
	}

	if (show_without_fzf) {
		printf "%s", output
		exit
	}

	fzf_preview_command = "exec " ENVIRON["_"] " -P {2}"
	fzf_command = "fzf --reverse --exact  --preview-window=down "\
				" --preview='" fzf_preview_command "' --nth=2 "\
				"--preview-window=down --multi <<EOF\n"\
				output "EOF"
	ERRNO = ""
	purge_cmd = "apt purge --autoremove --"

	nothing2remove= 1

	if ((fzf_command | getline) > 0) {
		purge_cmd = purge_cmd " " $2
		nothing2remove = 0
	}
	while ((fzf_command | getline) > 0) {
		purge_cmd = purge_cmd " " $2
	}
	fzf_command_code = close(fzf_command)
	if (fzf_command_code == 127 || fzf_command_code == 32512) {
		printf "%s", output
		exit
	}

	if (nothing2remove) {
		exit
	}

	system(purge_cmd)
}

BEGIN {
	process_options()
	while(1) {
		main()
	}
}
# vi:ft=awk noet
